apply from: "https://github.com/VeiZhang/build.gradle/blob/master/config.gradle?raw=true"
/*********************上传jCenter依赖*********************/
def plugins = rootProject.ext.plugins

/*********************读取配置文件中的key（保密）*********************/
def user = getPropertyValue('bintrayUser')
def key = getPropertyValue('bintrayKey')
def org = getPropertyValue('userOrg')

// 添加
// 为了上传多个module统一管理
ext {
    bintrayUser = user
    bintrayKey = key
    dryRun = 'false'
    userOrg = org
    groupId = 'com.excellence'
    // 默认版本 1.0.0，需要更改
    publishVersion = '1.0.0'
    // 关联链接，默认https://github.com/VeiZhang
    website = 'https://github.com/VeiZhang'
    licences = ['Apache-2.0']
}

/**读取bintray.key文件的key**/
def getPropertyValue(String key) {
    if (key == null || key.length() == 0)
        return null

    File file = project.rootProject.file('../../bintray.key')
    if (!file.exists())
        return null

    InputStream inputStream = file.newDataInputStream()
    Properties properties = new Properties()
    properties.load(inputStream)

    //读取Key
    return properties.getProperty(key)
}

/*********************上传jCenter的任务*********************/
apply plugin: plugins.novoda

/**避免中文注释:编码GBK的不可映射字符**/
tasks.withType(Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

/**避免Javadocs错误:找不到引用**/
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
}

/**发布到我的Bintray仓库**/
publish {
    bintrayUser = rootProject.bintrayUser
    bintrayKey = rootProject.bintrayKey
    dryRun = Boolean.parseBoolean(rootProject.dryRun)
    userOrg = rootProject.userOrg
    groupId = rootProject.groupId
    // 依赖名称必须填写，默认空
    artifactId = ''
    publishVersion = rootProject.publishVersion
    // 依赖描述，默认空
    desc = ''
    website = rootProject.website
    licences = rootProject.licences
}
